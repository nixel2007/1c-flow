
///////////////////////////////////////////////////////////////////////////////////////////////////
// Прикладной интерфейс

Процедура ЗарегистрироватьКоманду(Знач ИмяКоманды, Знач Парсер) Экспорт
	Команда = Парсер.ОписаниеКоманды(ИмяКоманды, "Инициализировать 1c-flow");
	Парсер.ДобавитьКоманду(Команда);
	Парсер.ДобавитьПозиционныйПараметрКоманды(Команда, "ПутьКСтволовомуХранилищу", "Путь к основному (стволовому) хранилищу.");
	Парсер.ДобавитьПозиционныйПараметрКоманды(Команда, "ПользовательСтволовогоХранилища", "Пользователь основного (стволового) хранилища.");
	Парсер.ДобавитьПозиционныйПараметрКоманды(Команда, "ПарольСтволовогоХранилища", "Пароль основного (стволового) хранилища.");
КонецПроцедуры

// Выполняет логику команды
// 
// Параметры:
//   ПараметрыКоманды - Соответствие ключей командной строки и их значений
//
Функция ВыполнитьКоманду(Знач ПараметрыКоманды) Экспорт
	
	ПутьКСтволовомуХранилищу = ПараметрыКоманды["ПутьКСтволовомуХранилищу"];
	ПользовательСтволовогоХранилища = ПараметрыКоманды["ПользовательСтволовогоХранилища"];
	ПарольСтволовогоХранилища = ПараметрыКоманды["ПарольСтволовогоХранилища"];
	
	Консоль = Новый Консоль;
	Если НЕ ЗначениеЗаполнено(ПутьКСтволовомуХранилищу) Тогда
		Консоль.ВывестиСтроку("Укажите путь к стволовому хранилищу:");
		
		ПутьКСтволовомуХранилищу = Консоль.ПрочитатьСтроку();
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ПользовательСтволовогоХранилища) Тогда
		Консоль.ВывестиСтроку("Укажите пользователя стволового хранилища:");
		
		ПользовательСтволовогоХранилища = Консоль.ПрочитатьСтроку();
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ПарольСтволовогоХранилища) Тогда
		Консоль.ВывестиСтроку("Укажите пароль стволового хранилища:");
		
		ПарольСтволовогоХранилища = Консоль.ПрочитатьСтроку();
	КонецЕсли;
	
	СохраняемыеПараметры = Новый СохраняемыеПараметры;
	
	ЧитатьИмеющийсяКонфиг = Ложь;
	СтруктураПараметров = СохраняемыеПараметры.Конструктор_СтруктураПараметров(ЧитатьИмеющийсяКонфиг);
	СтруктураПараметров.ПутьКСтволовомуХранилищу 		= ПутьКСтволовомуХранилищу;
	СтруктураПараметров.ПользовательСтволовогоХранилища = ПользовательСтволовогоХранилища;
	СтруктураПараметров.ПарольСтволовогоХранилища 		= ПарольСтволовогоХранилища;
	
	СохраняемыеПараметры.СохранитьПараметры(СтруктураПараметров);
	
	Возврат 0;
	
КонецФункции
